;;; -*- mode: Sawfish -*-

;;; Require stuff

(require 'sawfish.wm.commands.jump-or-exec)
(require 'sawfish.wm.util.selection)
(require 'sawfish.wm.util.window-order)
(require 'sawfish.wm.ext.match-window)
(require 'prompt)
(require 'prompt-menu)
(require 'emacs)
(require 'undo)
(require 'home-ws) ; Handy!


;;; Personal Settings

(setq *use-features* '(music org xmobar)) ;opacity
(setq *music-player* 'musicd) ; musicd, mpd or xmms2

(setq *workspace-count* 4) ; Amount of unnamed workspaces to create

(setq help-display-info-function emacs-call-info)


;;; Window Matchers

(add-window-matcher 'WM_CLASS "MPlayer" '(depth . 2) '(sticky . t))


;;; Keymaps

(defvar irc-keymap (make-keymap) "Keymap for IRC")

; IRC bindings
(bind-keys
 irc-keymap

 "M-i" '(jump-or-exec-or-last "screen -x" "urxvt -e sh -c 'ssh hashbox@10.1.1.9'")
 "M-p" '(irc (x-get-selection 'PRIMARY))
 "M-s" '(irc (prompt #:title "IRC:"))
 )

; alt-n to switch irssi windows
(do ((i 1 (1+ i)))
    ((< 9 i) i)
  (bind-keys
   irc-keymap
   (concat "M-" (number->string i))
   `(synthesize-event (concat "M-" (number->string ,i)) (get-window-by-name-re "screen -x"))))

; Global bindings
(bind-keys
 global-keymap

 "M-i"                 irc-keymap

 "M-tab"               '(display-window (prompt-for-window))

 "M-c"                 'delete-window
 "M-b"                 '(prompt-menu (emacs-buffers-menu) "Emacs Buffer:")
 "M-f"                 '(emacs-edit (prompt-for-file "Emacs Edit:"))
 "M-o"                 '(system (format nil "xdg-open %s &" (prompt-for-file "Open:")))
 "M-r"                 '(rename-workspace current-workspace (prompt #:title "Workspace Name:"))

 "M-w"                 '(jump-or-exec-or-last "Vimperator" "firefox")
 "M-e"                 '(jump-or-exec-or-last "Urgent Tasks" "emacs")

 "M-Ret"               '(system (concat (prompt #:title "Run:") " &"))
 "M-`"                 '(call-command (prompt-for-command "Sawfish Command:"))

 "M-u"                 'undo
 "M--"                 'undo

 "M-n"                 '(system "firefox &")
 "M-m"                 '(emacs-run "(make-frame)")

 "M-Space"             '(system "urxvt &")

 "Super-l"             '(system "i3lock -p default -c 000000")

 "M-="                 'maximize-fill-window-toggle

 "Super-Button4-Click" 'next-workspace
 "Super-Button5-Click" 'previous-workspace
 )

; Root window bindings
(bind-keys
 root-window-keymap

 "Button3-Click" 'popup-root-menu

; These are annoying, I find myself frequently scrolling through half of my workspaces
; with the slip of the mouse
;"Button4-Click" 'next-workspace
; "Button5-Click" 'previous-workspace
 )

; Window bindings
(bind-keys
 window-keymap

 "M-Button3-Move" 'resize-window-interactively
 )


;;; Macros

(defmacro emacs (form #!optional type)
  "Macro to allow passing a quoted form to be evaluated in Emacs"
  `(emacs-eval (format nil "%S" ,form) ,type))
  
(defmacro append! (list1 list2)
  "Destructive Append Macro"
  `(setq ,list1 (append ,list1 ,list2)))


;;; Custom functions

(define (jump-or-exec-or-last re exec)
  "Jump-or-exec, unless the window is already focused, in which case focus last window"
  (if (string-match re (window-name (input-focus)))
      (display-window (nth 1 (window-order)))
    (jump-or-exec re exec)))

(define (resize-current-window w h)
  "Resize the current window"
  (resize-window-with-hints* (input-focus) w h))

(define (rename-workspace n name)
  "Rename the Nth workspace to NAME"
  (setq workspace-names
        (let ((i -1))
          (mapcar #'(lambda (item)
                      (if (= n (setq i (1+ i)))
                          name
                        item)) workspace-names))))

(define (send-string-to-window s w)
  "What it says on the tin"
  (let ((i 0))
    (while (< i (length s))
           (let* ((ch (substring s i (setq i (1+ i))))
                  (e (cond ((equal ch "\n") "RET")
                           (t ch))))
             (synthesize-event e w)))))

(define (irc s)
  "Send string to IRC window, pressing enter"
  (send-string-to-window (concat s "\n") (get-window-by-name-re "screen -x")))

; http://www.mail-archive.com/sawfish-list@gnome.org/msg04711.html
(define (remove-duplicates input)
  "Remove duplicate entries from `input'"
  (do ((a '() (if (member (car input) a) a (cons (car input) a)))
       (input input (cdr input)))
      ((null input) (reverse a))))


;; Numbered Workspaces
(define (update-workspaces #!optional workspaces)
  "Create Workspaces (including org-mode tags)"
  (setq workspace-names '())
  (do ((i 1 (1+ i)))
      ((< *workspace-count* i) i)
    (append! workspace-names (list (number->string i))))
  (append! workspace-names (or workspaces
                               (emacs
                                '(mapcar (lambda (tag)
                                           (substring-no-properties (car tag)))
                                         (org-global-tags-completion-table)) 'parse))))

(define-command 'update-workspaces update-workspaces)

(update-workspaces)

(define (find-workspace name)
  "Finds a workspace by name"
  (let ((i 0))
    (while (and
            (> (length workspace-names) i)
            (not (equal (nth i workspace-names) name)))
      (setq i (1+ i)))
    i))

(define (goto-workspace name)
  "Switches workspace by name"
  (call-command 'activate-workspace (1+ (find-workspace name))))

; Workspace switching binds
(do ((i 1 (1+ i)))
    ((< (length workspace-names) i) i)
  (bind-keys
   global-keymap
   (concat "Super-" (number->string i))
   `(activate-workspace ,i)))

(define (get-window-memory-usage w)
  "Get Memory Usage for a Window using _NET_WM_PID"
  (let ((pid (caddr (get-x-property w '_NET_WM_PID)))
        (output (make-string-output-stream)))
    (if pid
        (progn
          (call-process
           (make-process output) nil
           "sh" "-c" (format nil "ps -p %s -o size=" (car (vector->list pid))))
          (string->number (string-replace "\012| " "" (get-output-stream-string output))))
      0)))

(defun human-size (file-size)
  "Return FILE-SIZE as a string in human-readable format using M, G, T."
  (format nil (cond
		((< file-size 1024.0) "%dKB")
		((< (setq file-size (round (/ file-size 1024.0))) 10.0) "%dMB")
		((< file-size 1024.0) "%dMB")
		((< (setq file-size (round (/ file-size 1024.0))) 10.0) "%dGB")
		((< file-size 1024.0) "%dGB")
		((< (setq file-size (round (/ file-size 1024.0))) 10.0) "%dTB")
		(t "%dTB"))
	  file-size))

(define (show-window-memory-usage w)
  "Show Memory Usage for a Window"
  (display-message (format nil "Window %S is using %s"
                           (window-name w) (human-size (get-window-memory-usage w)))))

(define-command 'memory-usage show-window-memory-usage #:spec "%W")


;;; Optional Features

;; Focus Transparency
(when (member 'opacity *use-features*)
  (require 'native-focus-transparency)
  (setq *out-of-focus-trans* 0xE0000000))


;; Org-mode Integration (http://demonastery.org/45/)
(when (member 'org *use-features*)
  (define (org-sawfish-clock-in-by-tag tag)
    "Tells org-mode to clock in to a task, by tag name"
    (emacs
     `(progn
        (org-clock-out t)
        (org-map-entries 'org-clock-in ,tag 'agenda))))
  
  ;; Clock prompt
  (define (org-sawfish-clock-prompt)
    "Prompt for a tag to clock in to"
    (let ((query (prompt-from-list (nthcdr *workspace-count* workspace-names) "Clock in to:" nil t)))
      (if query
          (org-sawfish-clock-in-by-tag query)
        (emacs '(org-clock-out t)))))

  ;; Workspace change hook
  (define (org-sawfish-enter-workspace-hook)
    (org-sawfish-clock-in-by-tag (nth current-workspace workspace-names)))

  (add-hook 'enter-workspace-hook org-sawfish-enter-workspace-hook)

  ;; Check for potentially clocked windows and remind user
  (setq *clocked-window-ignore* '("Emacs"))

  (define (is-clocked-window w)
    "Checks if the window looks like it should be clocked"
    (when (filter #'(lambda (x)
                      (not (or
                            (string-match x (window-name w) 0 t)
                            (string-match x (window-class w) 0 t))))
                  *clocked-window-ignore*)
      (let ((tags (nthcdr *workspace-count* workspace-names)))
        (when tags
          (filter #'(lambda (x)
                      (string-match x (window-name w) 0 t))
                  tags)))))

  (define (org-sawfish-focus-change-hook w)
    (let ((tags (is-clocked-window w)))
      (when (and tags (not (member (find-workspace (car tags)) (window-workspaces w))))
        (display-message
         (format nil "Looks like you're working on the %S task..\nYou should move this to a clocked workspace.."
                 (mapconcat #'identity tags " ")))
        (make-timer (lambda (x) (display-message nil) (delete-timer x)) 5))))

  (add-hook 'focus-in-hook org-sawfish-focus-change-hook)
  (add-hook 'property-notify-hook org-sawfish-focus-change-hook)

  ;; Function to prompt for tag to clock in to, or guess and move window to workspace
  (define (org-sawfish-start-clock w)
    "Moves current window to clocked workspace if applicable, or ask to clock"
    (let ((tags (is-clocked-window w)))
      (if tags
          (let ((workspace (find-workspace (car tags))))
            (move-window-to-workspace w (car (window-workspaces w)) workspace)
            (select-workspace workspace))
        (org-sawfish-clock-prompt))))

  (bind-keys global-keymap "Super-`" '(org-sawfish-start-clock (input-focus))))


;; Music Control
(when (member 'music *use-features*)
  (setq *music-list*
        (if (= *music-player* 'musicd)
            (sort (remove-duplicates
                   (append
                    (directory-files "/media/media/Music")
                    (directory-files "/media/media/Movies")
                    (directory-files "/media/media/TV Shows"))))
          (let ((output (make-string-output-stream)))
            (cond
             ((= *music-player* 'xmms2)
              (call-process (make-process output) nil "sqlite3" (concat (getenv "XDG_CONFIG_HOME") "/xmms2/medialib.db")
                            "SELECT DISTINCT value FROM Media WHERE key = \"artist\";"))
             ((= *music-player* 'mpd)
              (call-process (make-process output) nil "mpc" "list" "artist")))
            (sort (remove-duplicates
                   (string-split "\012" (string-downcase
                                         (get-output-stream-string output))))))))

  (define (music query)
    "Select artist for playing in Music player"
    (cond
     ((= *music-player* 'musicd)
      (system (format nil "%s/Documents/bin/play %s &" (user-home-directory) query)))
     ((= *music-player* 'xmms2)
      (if (string-match "[:~]" query)
          (system (format nil "nyxmms2 scap %s &" query))
        (system (format nil "nyxmms2 scap artist:'%s' &" query))))
     ((= *music-player* 'mpd)
      (system (format nil "%s/Documents/bin/artist %S" (user-home-directory) query)))))

  (define (prompt-for-music)
    "Prompt for a Music Query to pass to the Active Player"
    (let ((query (prompt-from-list *music-list* "Music:" nil t)))
      (when query
        (music query))))

  (bind-keys
   global-keymap

   "Super-Ret" '(prompt-for-music)
   ))


;; Festival
(when (member 'festival *use-features*)
  (require 'festival)
  (festival-open)

  (define (festival-say-with-voice text voice)
    "Say text using a specific voice"
    (festival-eval voice)
    (festival-say text)
    (festival-eval '(voice_nitech_us_slt_arctic_hts)))

  (festival-say-workspace-on-change t)

  (bind-keys
   global-keymap

   "Super-Button1-Click" '(festival-say-with-voice (x-get-selection 'PRIMARY) '(voice_nitech_us_rms_arctic_hts))
   "Super-Button3-Click" '(festival-say-with-voice (x-get-selection 'PRIMARY) '(voice_nitech_us_slt_arctic_hts))
   ))


;; Mouse Gestures
(when (member 'mouse-gestures *use-features*)
  (require 'grow-pack)

  (setq *gestures-initial-xy* '(0 . 0))

  (define (start-gesture)
    "Initialize gestures, must be called on mouse-down, before perform-gesture"
    (setq *gestures-initial-xy* (query-pointer)))

  (define (perform-gesture #!key threshold up down left right)
    "Perform a mouse-based gesture"
    (let* ((threshold (or threshold 60))
           (initial-x (car *gestures-initial-xy*))
           (initial-y (cdr *gestures-initial-xy*))
           (new-xy (query-pointer))
           (new-x (car new-xy))
           (new-y (cdr new-xy)))
      (cond
       ((> (- initial-y threshold) new-y)
        (eval (or up    '(grow-window-up (input-focus)))))
       ((> (- initial-x threshold) new-x)
        (eval (or left  '(grow-window-left (input-focus)))))
       ((< (+ initial-y threshold) new-y)
        (eval (or down  '(grow-window-down (input-focus)))))
       ((< (+ initial-x threshold) new-x)
        (eval (or right '(grow-window-right (input-focus))))))))

  (bind-keys
   global-keymap

   "Super-Button2-Click" '(start-gesture)
   "Super-Button2-Off"   '(perform-gesture)
   ))


;; Xmobar
(when (member 'xmobar *use-features*)
  (require 'xmobar)
  (activate-xmobar
   (concat (user-home-directory) "/.sawfish/xmobar")))
